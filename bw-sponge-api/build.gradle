// Apply plugins
apply plugin: 'java' // Support for Java
apply plugin: 'maven' // Maven integration
apply plugin: 'idea' // Idea integration
apply plugin: 'license' // Automatic license headers
apply plugin: 'com.github.johnrengelman.shadow' // JarShadow

// Maven group
group = 'it.kytech.bowwarfare.sponge'
version = '1.0-SNAPSHOT'

// Set compatibility
sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    maven {
        name 'Sponge maven repo'
        url 'http://repo.spongepowered.org/maven'
    }
}

dependencies {
    compile "org.spongepowered:spongeapi:2.1-SNAPSHOT"
}

defaultTasks 'clean', 'licenseFormat', 'build', 'shadowJar'

//Add a new dependency configuration
configurations {
    shadow
    compile {
        extendsFrom shadow
    }
}

// Include LICENSE.txt in all jars
processResources {
    from new File(rootProject.getProjectDir(), "LICENSE")
}

// Make the jar task contain all dependencies except for the provided configuration in the final output
shadowJar {
    configurations = [project.configurations.shadow]
    dependencies {
        exclude dependency('org.spongepowered:spongeapi')
    }
    classifier = ''
}

// Configure license headers
license {
    ext.name = rootProject.projectName
    ext.author = rootProject.author
    ext.url = rootProject.authorUrl
    ext.year = rootProject.inceptionYear

    header rootProject.file('HEADER')

    mapping {
        java = 'JAVADOC_STYLE'
    }
}


task sourceJar(type: Jar) {
    from sourceSets.main.java
    from sourceSets.main.resources
    classifier = ''
}

// Define compiled jar, sources jar and javadoc jar as Maven artifacts
artifacts {
    archives shadowJar
    archives sourceJar
}