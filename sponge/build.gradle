// Apply plugins
apply plugin: 'java' // Support for Java
apply plugin: 'maven' // Maven integration
apply plugin: 'idea' // Idea integration
apply plugin: 'license' // Automatic license headers

version = '1.8.0-1.0'

// Set compatibility
sourceCompatibility = 1.7
targetCompatibility = 1.7

// Maven group
group = 'it.kytech.bowwarfare.sponge'

// Add a new dependency configuration
configurations {
    provided
    compile.extendsFrom provided
}

repositories {
    maven {
        name 'Sponge maven repo'
        url 'http://repo.spongepowered.org/maven'
    }
}

dependencies {
    compile project(':sponge-api') // Include the API in the output jar
    provided "org.spongepowered:spongeapi:1.1-SNAPSHOT"
}

// Make the jar task contain all dependencies except for the provided configuration in the final output
jar {
    dependsOn configurations.runtime
    from {
        (configurations.runtime - configurations.provided).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

// Include LICENSE.txt in all jars
processResources {
    from new File(rootProject.getProjectDir(), "LICENSE")
}

// Configure license headers
license {
    ext.name = rootProject.projectName
    ext.author = rootProject.author
    ext.url = rootProject.authorUrl
    ext.year = rootProject.inceptionYear

    header rootProject.file('HEADER')

    mapping {
        java = 'JAVADOC_STYLE'
    }
}

// Define compiled jar, sources jar and javadoc jar as Maven artifacts
artifacts {
    archives jar
}